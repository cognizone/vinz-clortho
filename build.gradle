plugins {
  id 'pl.allegro.tech.build.axion-release' version '1.13.3'
  id 'org.springframework.boot' version '2.6.6' apply false
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id 'java-library'
  id 'maven-publish'
  id "io.freefair.lombok" version "6.5.1"
}

scmVersion {
  tag {
    prefix = "v"
    versionSeparator = ""
    branchPrefix = [
            'release/.*': 'release-v',
            'hotfix/.*' : 'hotfix-v'
    ]
  }
  nextVersion {
    suffix = "SNAPSHOT"
    separator = "-"
  }
}

group = 'zone.cogni.vinz-clortho'
version = scmVersion.version
sourceCompatibility = '11'

dependencyManagement {
  imports {
    mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
  }
}

ext {
  commonsIoVersion = "2.11.0"
  commonsLang3Version = "3.12.0"
  httpclientVersion = "4.5.13"
}

repositories {
  mavenCentral()
}

dependencies {
  annotationProcessor 'org.projectlombok:lombok'
  compileOnly 'org.projectlombok:lombok'

  implementation 'org.springframework.boot:spring-boot-starter-web'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'

  implementation "commons-io:commons-io:${commonsIoVersion}"
  implementation "org.apache.commons:commons-lang3:${commonsLang3Version}"
  implementation "org.apache.httpcomponents:httpclient:${httpclientVersion}"
}

tasks.named('test') {
  useJUnitPlatform()
}

task sourcesJar(type: Jar) {
  description = 'Creates sources JAR.'
  archiveClassifier = 'sources'

  from sourceSets.main.allSource
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = group
      artifactId = 'vinz-clortho'
      version = scmVersion.version
      from components.java
      artifact sourcesJar
    }
  }
  repositories {
    maven {
      credentials {
        username = System.properties['archiva.user']
        password = System.properties['archiva.password']
      }
      name = 'public'
      url = System.properties['archiva.url']
    }
  }
}
