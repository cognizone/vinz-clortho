buildscript {
  ext {
    commonsIoVersion = "2.11.0"
    commonsLang3Version = "3.12.0"
    httpclientVersion = "4.5.13"
    jsr305Version = "3.0.2"
    junitVersion = "5.9.2"
    assertJVersion = "3.24.2"
    mockitoVersion = "5.3.1"
  }
}

plugins {
  id 'pl.allegro.tech.build.axion-release' version '1.13.3'
  id 'org.springframework.boot' version '2.6.14' apply false
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'idea'
  id 'java'
  id 'java-library'
  id 'maven-publish'
  id "io.freefair.lombok" version "6.5.1"
}

repositories {
  mavenLocal()
  mavenCentral()
}

scmVersion {
  tag {
    prefix = "v"
    versionSeparator = ""
    branchPrefix = [
            'release/.*': 'release-v',
            'hotfix/.*' : 'hotfix-v'
    ]
  }
  nextVersion {
    suffix = "SNAPSHOT"
    separator = "-"
  }
}

idea {
  project {
    jdkName = "11"
    languageLevel = "11"
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: "io.freefair.lombok"
  apply plugin: 'io.spring.dependency-management'
  apply plugin: "maven-publish"

  group = 'zone.cogni.vinz-clortho'
  project.version = scmVersion.version
  sourceCompatibility = '11'

  repositories {
    if (project.hasProperty('jenkins-ci')) {
      maven {
        url "${System.properties['nexus.url']}/repository/cognizone-group"
        credentials {
          username = System.properties['nexus.username']
          password = System.properties['nexus.password']
        }
        allowInsecureProtocol(true)
      }
    }
    mavenLocal()
    mavenCentral()
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://archiva.cogni.zone/repository/public" }
  }

  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
    dependencies {
      dependency("javax.inject:javax.inject:1")
      dependency("com.google.code.findbugs:jsr305:${jsr305Version}")
      dependency("commons-io:commons-io:${commonsIoVersion}")
      dependency("org.apache.commons:commons-lang3:${commonsLang3Version}")
      dependency("org.apache.httpcomponents:httpclient:${httpclientVersion}")

      dependency("org.junit.jupiter:junit-jupiter:${junitVersion}")
      dependency("org.assertj:assertj-core:${assertJVersion}")
      dependency("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
    }
  }

  dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
  }

  tasks.named('test') {
    useJUnitPlatform()
  }

  task sourcesJar(type: Jar) {
    description = 'Creates sources JAR.'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
  }


  publishing {
    publications {
      mavenJava(MavenPublication) {
        groupId = group
        version = scmVersion.version
        artifact jar
        artifact sourcesJar
      }
    }
    repositories {
      if (project.hasProperty("publishToCognizoneArchiva")) {
        maven {
          credentials {
            username = System.properties['archiva.username']
            password = System.properties['archiva.password']
          }
          name = 'cognizone'
          url = System.properties['archiva.url']
        }
      }
      if (project.hasProperty("publishToCognizoneNexus")) {
        maven {
          credentials {
            username = System.properties['nexus.username']
            password = System.properties['nexus.password']
          }
          def releasesRepoUrl = "${System.properties['nexus.url']}/repository/cognizone-release"
          def snapshotsRepoUrl = "${System.properties['nexus.url']}/repository/cognizone-snapshot"
          url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
          allowInsecureProtocol(true)
        }
      }
    }
  }
}


project("vinz-clortho") {
  dependencies {
    //implementation("javax.inject:javax.inject:1") //if we don't have this dependency the publication build does not work???
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation "com.google.code.findbugs:jsr305"
    implementation "commons-io:commons-io"
    implementation "org.apache.commons:commons-lang3"
    implementation "org.apache.httpcomponents:httpclient"

    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation("javax.inject:javax.inject")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.assertj:assertj-core")
    testImplementation("org.mockito:mockito-junit-jupiter")
  }
}
