buildscript {
  ext {
    commonsIoVersion = "2.11.0"
    commonsLang3Version = "3.12.0"
    httpclientVersion = "4.5.13"
  }
}

plugins {
  id 'pl.allegro.tech.build.axion-release' version '1.13.3'
  id 'org.springframework.boot' version '2.6.14' apply false
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'idea'
  id 'java'
  id 'java-library'
  id 'maven-publish'
  id "io.freefair.lombok" version "6.5.1"
}

scmVersion {
  tag {
    prefix = "v"
    versionSeparator = ""
    branchPrefix = [
            'release/.*': 'release-v',
            'hotfix/.*' : 'hotfix-v'
    ]
  }
  nextVersion {
    suffix = "SNAPSHOT"
    separator = "-"
  }
}

idea {
  project {
    jdkName = "11"
    languageLevel = "11"
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: "io.freefair.lombok"
  apply plugin: 'io.spring.dependency-management'
  apply plugin: "maven-publish"

  group = 'zone.cogni.vinz-clortho'
  version = scmVersion.version
  sourceCompatibility = '11'

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://archiva.cogni.zone/repository/public" }
  }

  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
    dependencies {
      dependency("commons-io:commons-io:${commonsIoVersion}")
      dependency("org.apache.commons:commons-lang3:${commonsLang3Version}")
      dependency("org.apache.httpcomponents:httpclient:${httpclientVersion}")
    }
  }

  dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
  }

  tasks.named('test') {
    useJUnitPlatform()
  }

  task sourcesJar(type: Jar) {
    description = 'Creates sources JAR.'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
  }


  publishing {
    repositories {
      maven {
        credentials {
          username = System.properties['archiva.user']
          password = System.properties['archiva.password']
        }
        name = 'public'
        url = System.properties['archiva.url']
      }
    }
    publishing {
      publications {
        mavenJava(MavenPublication) {
          groupId = group
          //artifactId = 'vinz-clortho'     //it will take the module name -> all fine
          version = scmVersion.version
          artifact jar
          artifact sourcesJar
        }
      }
    }
  }
}


project("vinz-clortho") {
  dependencies {
    //implementation("javax.inject:javax.inject:1") //if we don't have this dependency the publication build does not work???
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation "commons-io:commons-io"
    implementation "org.apache.commons:commons-lang3"
    implementation "org.apache.httpcomponents:httpclient"
  }
}
